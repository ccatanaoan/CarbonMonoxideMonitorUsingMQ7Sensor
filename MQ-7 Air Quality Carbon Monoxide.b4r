Version=2.2
NumberOfModules=0
Build1=Default,B4RDev
NumberOfFiles=0
NumberOfLibraries=1
Library1=rcore
@EndOfDesignText@
' Carbon Monoxide (CO) Monitor Using MQ-7 sensor
'
' I used the following sites To help me reach the outcome:
'
' Gas Sensor "Carbon Monoxide" - MQ-7 aka "Flying-Fish" - https://www.hackster.io/ingo-lohs/gas-sensor-carbon-monoxide-mq-7-aka-flying-fish-e58457
' ARDUINO CO MONITOR USING MQ-7 SENSOR - http://www.instructables.com/id/Arduino-CO-Monitor-Using-MQ-7-Sensor/

' MQ-7 Carbon Monoxide Sensor: It is sensitive To gases like Alcohol (hand sanitizer), Butane (a lighter) And Difluoroethane (compressed "air" duster), among other gasses. 
' Datasheet found here: http://cvrr.ucsd.edu/ece156/ECE156Sensors/Carbon_Monoxide.pdf

'     MQ-7 Sensor    ->      ESP8266
'         A0         ->        A0
'         D0         ->        D0
'         VCC        ->        VIN (5.5V)
'         GRD        ->        GRD

#Region Project Attributes
	#AutoFlushLogs: True
	#CheckArrayBounds: True
	#StackBufferSize: 300
#End Region

Sub Process_Globals
	Public Serial1 As Serial
	Private MQ7Pin As Pin                 'Output pin connected from the MQ-7 sensor
	Private MQ7PinNumber As Byte = 0x00   'Pin number used is A0 (Analog)
End Sub

Private Sub AppStart
	Serial1.Initialize(115200)
	Delay(3000)
	Log("AppStart")
	
	' Init the pin with MQ-7 connected
	MQ7Pin.Initialize(MQ7PinNumber, MQ7Pin.MODE_OUTPUT)

	CheckAirQuality(0)
End Sub

Sub CheckAirQuality(tag As Byte)
	' 1) Preparation
	Log("Turn the heater fully on")
	MQ7Pin.AnalogWrite(255) ' HIGH = 255
	Log("Heat for 1 min")
	Delay(60000)
	Log("Now reducing the heating power: turn the heater to approx 1.4V")
	MQ7Pin.AnalogWrite(71.4) ' 255x1400/5000
	Log("Heat for 90 sec")
	Delay(90000)
	
	' 2) Reading
	Log("CO2 via MQ7: we need to read the sensor at 5V, but must not let it heat up. So hurry!")
	MQ7Pin.AnalogWrite(255)
	Delay(50) ' Getting an analog read apparently takes 100uSec
	Dim rawvoltage As UInt = MQ7Pin.AnalogRead
	
	' 3) Print the results to the serial monitor
	Log("*************************")
	Log("MQ-7 PPM: ",rawvoltage)
	
	' 4) Interpretation
	' Detecting range: 20ppm-2000ppm carbon monoxide
	' Air quality-cases: < 200 perfect, 200 - 800 normal, > 800 - 1800 high, > 1800 abnormal
	If rawvoltage <= 200 Then
		Log("Air-Quality: CO perfect")
	else if ((rawvoltage > 200) And (rawvoltage < 800)) Or rawvoltage = 800 Then
		Log("Air-Quality: CO normal")
	else if ((rawvoltage > 800) And (rawvoltage < 1800)) Or rawvoltage = 1800 Then
		Log("Air-Quality: CO high")
	else If rawvoltage > 1800 Then
		Log("Air-Quality: ALARM CO very high")
	Else
		Log("MQ-7 - cant read any value - check the sensor!")
	End If
	Log("*************************")
	CallSubPlus("CheckAirQuality",1000,0)
End Sub
